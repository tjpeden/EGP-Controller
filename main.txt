@name EGP Main Controller
@inputs E:wirelink Reset
@persist Owner:entity Self:entity
@persist Controllers:array Buttons:array Map:table
@persist NextID

#[
    This is a sort of MVC style way of writing EGP
    "Apps" where this controller acts as a protal
    to the others.
    
    Dependencies: player_use_key.txt
]#

# More of a lazy trick really
function void wirelink:egpDimensions(ObjectID) {
    TopLeft = This:egpPos(ObjectID)
    BottomRight = TopLeft + This:egpSize(ObjectID)
}

# Creates a button where the text is centered inside a Width x 30 rounded box
function void wirelink:egpButton(Text:string, Position:vector2, Width) {
    ButtonID = NextID, NextID++
    TextID = NextID, NextID++
    
    This:egpRoundedBoxOutline(ButtonID, Position, vec2(Width,30))
    This:egpText(TextID, Text, vec2(0,0))
    This:egpAlign(TextID, 1, 1)
    This:egpParent(TextID, ButtonID)
    
    # We keep a list of buttons to track hover and click events
    Buttons:pushNumber(ButtonID)
}

# On mouse enter, highlight button text (ButtonID + 1 = TextID)
function void wirelink:egpHighlight(ButtonID) {
    This:egpColor(ButtonID + 1, vec(0, 255, 0))
}

# On mouse exit, unhighlight button text (ButtonID + 1 = TextID)
function void wirelink:egpUnhighlight(ButtonID) {
    This:egpColor(ButtonID + 1, vec(255, 255, 255))
}

# Quick function for creating the cursor
function void wirelink:egpCreateCursor(ObjectID) {
    This:egpCircle(ObjectID, vec2(256,256), vec2(5,5))
    This:egpParentToCursor(ObjectID)
    This:egpColor(ObjectID, vec(255, 0, 0))
}

# Utility method on the entity class
function normal entity:lookingAt(Other:entity) {
    return This:aimEntity() == Other
}

# Utitlity method on the array class
function number array:last() {
    return This[This:maxIndex(), number]
}

# This method signals another controller to take over execution
function void run(Name:string) {
    dsSend(Name, "EGP.Portal", entity())
    dsSend("Bind.Use", "DSS.Events", 2, 0)
    runOnTick(0)
}

# Record registration of a subcontroller
function void register(Name:string) {
    Exists = 0
    for(I = Controllers:minIndex(), Controllers:maxIndex()) {
        if(Controllers[I, string] == Name) {
            Exists = 1
            break
        }
    }
    
    if(!Exists) { Controllers:pushString(Name) }
}

# Draw/redraw buttons (used when new controllers register)
function void createButtons() {
    for(I = Controllers:minIndex(), Controllers:maxIndex()) {
        N = I -1
        
        Name = Controllers[I, string]
        E:egpButton(Name, vec2(floor(N / 16) * 128, N * 32), 126)
        Map[Buttons:last(), string] = Name
    }
    E:egpOrder(1, NextID - 1)
}

if(~Reset&Reset) {
    dsSend("Halt", "EGP.Portal", entity())
}

# Setup for main controller execution
if(~Reset&Reset | first() | dsClk("Back")) {
    runOnTick(1)
    
    # Register player use key events
    dsSend("Bind.Use", "DSS.Events", 2, 1)
    
    NextID = 1
    Buttons = array()
    Map = table()
    
    # Make sure we have full control of what being displayed
    E:egpClear()
    E:egpDrawTopLeft(1)
    
    ID = NextID, NextID++
    E:egpCreateCursor(ID)
    
    createButtons()
}

# Setup global persistant variables
if( first() ) {
    dsJoinGroup("EGP.Portal") # Join control group
    # When updating firmware, let subcontrollers know to re-register
    dsSend("Update", "EGP.Portal", entity())
    
    Owner = owner()
    Controllers = array()
}

# Registration handler
if(dsClk("Register")) {
    Name = dsGetString()
    register(Name)
    exit()
}

# Stop execution until we are connected to the EGP Screen
if(!Self) {
    if(->E) { Self = E:entity() }
    exit()
}

# Update for newly registered controllers
if(Controllers:count() > Map:count()) { createButtons() }

# Check for and act on hover events
if( Owner:lookingAt(Self) ) {
    E:egpPos(1, vec2())
    Cursor = E:egpCursor(Owner)
    
    for(I = Buttons:minIndex(), Buttons:maxIndex()) {
        ID = Buttons[I, number]
        E:egpDimensions(ID)
        
        if(inrange(Cursor, TopLeft, BottomRight)) {
            E:egpHighlight(ID)
        } else {
            E:egpUnhighlight(ID)
        }
    }
}

# Check for and act on player use key events
if(dsClk("Use.Triggered")) {
    Cursor = E:egpCursor(Owner)
    
    for(I = Buttons:minIndex(), Buttons:maxIndex()) {
        ID = Buttons[I, number]
        E:egpDimensions(ID)
        
        if(inrange(Cursor, TopLeft, BottomRight)) {
            if(Map:exists(ID)) { run(Map[ID, string]) }
        }
    }
}
